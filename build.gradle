apply plugin: 'com.android.application'
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://maven.sms.co.at/content/repositories/thirdparty"
    }
    maven {
        url "http://maven.sms.co.at/content/repositories/releases"
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        versionName '2.8-SNAPSHOT'
        versionCode 18
        
        minSdkVersion 9
        targetSdkVersion 19
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
        }

        instrumentTest.setRoot('tests')
    }
    signingConfigs {
        release
    }
  
    if (project.hasProperty('buildNumber')) defaultConfig.versionName += '-' + buildNumber
    if (project.hasProperty('versionCode')) defaultConfig.versionCode = Integer.parseInt(versionCode)
  
    if (project.hasProperty('storeFile')) {
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }

    productFlavors {
        play {}
        amazon {} 
    }

    dependencies {
        compile 'com.mysms.android:mysms-android-theme-base:1.2.9'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def apk = output.outputFile;
                def newName = "mysms-theme-system-" + variant.baseName.replace("-release", "") + "-" + defaultConfig.versionName + ".apk";
                output.outputFile = new File(apk.parentFile, newName);
            }
        }
    }
}
